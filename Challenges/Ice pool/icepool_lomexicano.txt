toggle(@mexican_pool);
log(" ");
if(!@mexican_pool);
	keyup(left);
	keyup(right);
	keyup(forward);
	keyup(back);
	keyup(jump);
	keyup(sneak);
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &bMexican Pool&f: &cOFF&4!");
	stop("MEXICAN_POOL");
	stop;
endif;
log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &bMexican Pool&f: &aON&2!");

//Checking for problems in general;
unset(problem);
unsafe(0);
	//Checking if the pool is empty;
	for(#dx,-3,3);
		for(#dz,-13,-1);
			getidrel(%#dx%,-1,%#dz%,&id);
			if(&id != "air");
				log(" ");
				log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cPool isn't empty!");
				set(problem);
				break;
			endif;
		next;
		if(problem);
			break;
		endif;
	next;
	

	//Checking if the two sides (Z-axis) of the pool have blocks;
	unset(#dx[]);
	push(#dx[],"-4");
	#dx[1] = #dx[0] * -1;		
	foreach(#dx[],#dx);
		for(#dz,-14,0);
			getidrel(%#dx%,-1,%#dz%,&id);
			if(&id == "air");
				log(" ");
				log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cPool outline (Z-axis) isn't complete!");
				set(problem);
				break;
			endif;
		next;
		if(problem);
			break;
		endif;
	next;

	//Checking if the other two sides (X-axis) of the pool have blocks;
	unset(#dz[]);
	push(#dz[],"0");
	#dz[1] = #dz[0] - 14;
	foreach(#dz[],#dz);
		for(#dx,-3,3);
			getidrel(%#dx%,-1,%#dz%,&id);
			if(&id == "air");
				log(" ");
				log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cPool outline (X-axis) isn't complete!");
				set(problem);
				break;
			endif;
		next;
		if(problem);
			break;
		endif;
	next;
	
	//Checking if the chest is in the right place;
	getidrel(-4,0,0,&chest);
	if((&chest != "chest")&&(&chest != "trapped_chest")&&(&chest != "ender_chest"))
		log(" ");
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cChest missing!");
		set(problem);
	endif;
endunsafe;

if(problem);
	log(" ");
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cLooks like you've got some problems...");
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &bMexican Pool&f: &cOFF&4!");
	unset(@mexican_pool);
	stop("MEXICAN_POOL");
	stop;
endif;

//Opening the chest;
do();
	looks(270,16,0.2);
	keyup(sneak);
	key(use);
	do(10);
		wait(250ms);
	until(GUI == "GUICHEST");
until(GUI == "GUICHEST")
wait(5t);

//Picking up the ices we need;
set(#ice_needed,"91");
unsafe;
	for(#slot,27,62);
		getslotitem(%#slot%,&id,#stack);
		if(&id == "ice");
			dec(#ice_needed,"%#stack%");
		endif;
	next;
	if(#ice_needed > 0);
		for(#slot,0,26);
			getslotitem(%#slot%,&id,#stack);
			if(&id == "ice");
				slotclick(%#slot%,l,true);
				wait(1t);
				dec(#ice_needed,"%#stack%");
			endif;
			if(#ice_needed <= 0);
				wait(5t);
				break;
			endif;
		next;
	endif;
endunsafe;
gui();
if(#ice_needed > 0);
	log(" ");
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cLooks like you don't have enough ice for this construction, chap!");
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &bMexican Pool&f: &cOFF&4!");
	unset(@mexican_pool);
	stop("MEXICAN_POOL");
	stop;
endif;

//Saving coordinates;
set(#start_x,"%XPOS%");
set(#start_z,"%ZPOS%");

//Creating some integer arrays for 'looking' commands;
unset(#front_ice_pitch[]);
push(#front_ice_pitch[],"35");
push(#front_ice_pitch[],"50");
push(#front_ice_pitch[],"70");

unset(#side_ice_pitch[]);
push(#side_ice_pitch[],"32");
push(#side_ice_pitch[],"40");
push(#side_ice_pitch[],"55");

unset(#side_ice_yaw[]);
push(#side_ice_yaw[],"270");
push(#side_ice_yaw[],"90");


looks(21,28,0.6);
keydown(sneak);
keydown(forward);
wait(10t);

//Making 3 little bridges 4 blocks ahead of the player;
for(#forward,1,3);
	looks(21,28,0.1);
	keydown(sneak);
	keydown(forward);

	set(moving_right);
	unset(ok);
	do();
		looks(21,28,0.1);
		keydown(sneak);
		keydown(forward);
		pick(ice);
		if(((moving_right)&&(XPOS >= (#start_x + 3)))||((!moving_right)&&(XPOS <= (#start_x - 3))));
			//log("toggling");
			toggle(moving_right);
		endif;
		if(moving_right);
			keyup(left);
			keydown(right);
			keydown(forward);
		else;
			keyup(forward);
			keyup(right);
			keydown(left);
		endif;

		if((HITZ == (ZPOS - 3))&&(HITSIDE == "W"));
			//log("place it");
			if(HITX == (#start_x + 1));
				set(ok);
			endif;
			if(ITEM != "ice");
				pick(ice);
				wait(5t);
			endif;
			key(use);
			unset(moving_right);
		endif;
	until(ok);
	
	do();
		look(0,35);
		keyup(left);
		keydown(sneak);
		keydown(right);
	until(XPOS == #start_x);
	keyup(right);
	keyup(left);

	foreach(#front_ice_pitch[],#pitch);
		if(ITEM != "ice");
			pick(ice);
			wait(5t);
		endif;
		look(0,%#pitch%,0.3);
		key(use);
	next;

	#last_z = #start_z - (1 + 4 * #forward);
	do();
		look(0,45,0.1);
		keydown(forward);
		if((ZPOS - #last_z) > 1);
			keyup(sneak);
		else;
			keydown(sneak);
		endif;
	until((HITZ == (#start_z - 14))||(ZPOS == #last_z));
next;
keyup(forward);

//Place the last needed ice in the middle of the pool; 
set(down);
do();
	keydown(sneak);
	if(ITEM != "ice");
		pick(ice);
		wait(5t);
	elseif((HITY == (YPOS - 1))&&(HITZ == (#start_z - 14))&&(HITSIDE == "S"));
		key(use);
		break;
	elseif((PITCH > 84)&&(PITCH < 360));
		unset(down);
	elseif(PITCH < 25);
		set(down);
	elseif(down);
		looks(,+6,0.1);
	else;
		looks(,-6,0.1);
	endif;
loop;

//Placing the player in the middle of the block, between .300 and .700 (X-axis);
do();
	keydown(sneak);
	match(%XPOSF%,"(?<=\.)\d+$",#decimal_x);
	if((#decimal_x > 300)&&(#decimal_x < 700));
		log("&aCentered! &8[&a%#decimal_x%&8]");
		keyup(left);
		keyup(right);
		break;
	elseif(((XPOS < 0)&&(#decimal_x > 700))||((XPOS > 0)&&(#decimal_x < 300)));
		keyup(left);
		keydown(right);
	else;
		keyup(right);
		keydown(left);
	endif;
	wait(2t);
	keyup(left);
	keyup(right);
	wait(7t);
loop;

//Going sideways to the last ice block;
keydown(sneak);
do();
	looks(%#side_ice_yaw[0]%,%#side_ice_pitch[0]%,0.3);
	keydown(right);
until(ZPOS == (#start_z - 13));
keyup(right);
keyup(sneak);

//Place the reamining ices;
do();
	foreach(#side_ice_yaw[],#yaw,#yaw_index);
		getidrel(1,-1,0,&ice_ground);
		if((&ice_ground == "ice")&&(#yaw_index == 1));
		else;
			foreach(#side_ice_pitch[],#pitch);
				looks(%#yaw%,%#pitch%,0.2);
				if(ITEM != "ice");
					pick(ice);
					wait(5t);
				endif;
				key(use);
			next;
		endif;
	next;
	#z_now = ZPOS;
	keydown(sneak);
	getidrel(1,-1,1,&ice_ground);

	do();
		looks(270,32,0.1);
		keydown(left);
	until(ZPOS != (#z_now));
	keyup(left);
	keyup(right);

	if((XPOS == #start_x)&&(ZPOS == #start_z));
		break;
	endif;
loop;

//Look what you've done, you monster;
looks(0,15,1);
TOAST("Challenge","ice","Â§bCool");

unset(@mexican_pool);
keyup(left);
keyup(right);
keyup(forward);
keyup(back);
keyup(jump);
//keyup(sneak);
log(" ");
log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &bMexican Pool&f: &cOFF&4!");
stop("MEXICAN_POOL");
stop;